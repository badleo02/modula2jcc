//----------------------------------------------//
//		SINTAXIS GENERAL		//
//----------------------------------------------//
UnidadDeCompilacion:
ModuloPrograma 
ModuloDefinicion 
ModuloImplementacion

ModuloPrograma:
CabeceraModulo Bloque Identificador . 

ModuloDefinicion:
CabeceraDefinicion { DefinicionDeModulo } END Identificador .

ModuloImplementacion:
IMPLEMENTATION ModuloPrograma

CabeceraModulo:
MODULE Identificador [ Prioridad ] ; { ListaImportados ; }

CabeceraDefinicion:
DEFINITION MODULE Identificador ; { ListaImportados ; } [ ListaExportados ; ]

ListaImportados:
[ FROM Identificador ] IMPORT IdentDeProc { , IdentDeProc }

ListaExportados:
EXPORT [ QUALIFIED ] ListaDeIdentificadores

Prioridad:
ExpresionConstante

Bloque:
ParteDeclarativa ParteEjecutiva END

DefinicionDeModulo:
DeclaracionDeConstantes
TYPE { Identificador [ = EsquemaDeTipo ] ; }
DeclaracionDeVariables
CabeceraSubprograma ;

//----------------------------------------------//
//		DECLARACIONES BASICAS	   	//
//----------------------------------------------//
ParteDeclarativa:
{ Declaracion }

Declaracion:
DeclaracionDeConstantes
DeclaracionDeTipos
DeclaracionDeVariables
DeclaracionSubprograma
DeclaracionModulo

DeclaracionDeConstantes:
CONST { AsociacionConstante ; }

AsociacionConstante:
Identificador = ExpresionConstante _action_AsociacionConstante

DeclaracionDeTipos:
TYPE { DefinicionDeTipo ; }

DefinicionDeTipo:
Identificador = EsquemaDeTipo _action_DefinicionDeTipo

EsquemaDeTipo:
TipoSimple 
TipoConjunto
TipoFormacion
TipoRegistro
TipoPuntero
TipoSubprograma

TipoSimple:
TipoEnumerado _action_TipoSimple_Enumerado
IdentificadorDeTipo RestoDeTipo 
\[ ExpresionConstante .. ExpresionConstante \] // Subrango

TipoEnumerado:
\( ListaDeIdentificadores \) _action_TipoEnumerado

RestoDeTipo:
_epsilon_ // Simple
\[ ExpresionConstante .. ExpresionConstante \] // Subrango

TipoConjunto:
SET OF TipoSimple _action_TipoConjunto

TipoFormacion:
ARRAY TipoSimple { , TipoSimple } OF EsquemaDeTipo

TipoRegistro:
RECORD SecuenciaDeListasDeCampos END

SecuenciaDeListasDeCampos:
ListaDeCampos { ; ListaDeCampos }

ListaDeCampos:
_epsilon_
ListaDeIdentificadores : EsquemaDeTipo

TipoPuntero:
POINTER TO EsquemaDeTipo _action_TipoPuntero

TipoSubprograma:
PROCEDURE [ ListaDeTiposFormales ]

ListaDeTiposFormales:
( [ [ VAR ] TipoFormal { , [ VAR ] TipoFormal } ] ) [ : IdentificadorDeTipo ]

DeclaracionDeVariables:
VAR { ListaDeVariables }

ListaDeVariables:
ListaDeIdentificadores : EsquemaDeTipo ; _action_DeclaracionVariables

DeclaracionSubprograma:
CabeceraSubprograma ; Bloque Identificador ;

CabeceraSubprograma:
PROCEDURE Identificador [ ParametrosFormales ]

ParametrosFormales:
( [ GrupoDeParametros { ; GrupoDeParametros } ] ) [ : IdentificadorDeTipo ]

GrupoDeParametros:
[ VAR ] ListaDeIdentificadores : TipoFormal

DeclaracionModulo:
MODULE Identificador [ ExpresionConstante ] ; { ListaImportados ; } [ ListaExportados ; ] Bloque Identificador ;

//----------------------------------------------//
//		SENTENCIAS EJECUTABLES	   	//
//----------------------------------------------//
ParteEjecutiva:
[ BEGIN SecuenciaDeSentencias ]

SecuenciaDeSentencias:
Sentencia { ; Sentencia }

Sentencia:
_epsilon_
SentenciaAsignacion
SentenciaIF
SentenciaCASE
SentenciaWHILE
SentenciaREPEAT
SentenciaLOOP
SentenciaFOR
SentenciaWITH
LlamadaProcedimientoPredefinido
SentenciaRETURN
EXIT

SentenciaAsignacion:
Identificador RestoSentenciaAsignacion 

RestoSentenciaAsignacion:
ParteIzquierda RestoSentenciaRestoAsignacion

RestoSentenciaRestoAsignacion:
_epsilon_
:= Expresion 
ParametrosDeLlamada

ParteIzquierda:
_epsilon_
\[ ListaDeExpresiones \] ParteIzquierda
. IdentificadorOProcPredef ParteIzquierda
^ ParteIzquierda

IdentificadorOProcPredef:
Identificador
LlamadaProcedimientoPredefinido

SentenciaIF:
IF Expresion THEN SecuenciaDeSentencias RestoSentenciaIF END

RestoSentenciaIF:
{ ELSIF Expresion THEN SecuenciaDeSentencias } [ ELSE SecuenciaDeSentencias ]

SentenciaCASE:
CASE Expresion OF Caso { | Caso } [ ELSE SecuenciaDeSentencias ] END

Caso:
ListaDeValores : SecuenciaDeSentencias

SentenciaWHILE:
WHILE Expresion DO SecuenciaDeSentencias END 

SentenciaREPEAT:
REPEAT SecuenciaDeSentencias UNTIL Expresion 

SentenciaLOOP:
LOOP SecuenciaDeSentencias END 

SentenciaFOR:
FOR Identificador := Expresion TO Expresion [ BY ExpresionConstante ] DO SecuenciaDeSentencias END

SentenciaWITH:
WITH Identificador ParteIzquierda DO SecuenciaDeSentencias END 

SentenciaRETURN:
RETURN [ Expresion ] 

//----------------------------------//
//		EXPRESIONES			//
//----------------------------------//
ListaDeValores:
Valores RestoListaValores 

RestoListaValores:
_epsilon_ 
, Valores RestoListaValores 

Valores:
ExpresionConstante RestoValores

RestoValores:
_epsilon_
.. ExpresionConstante

ExpresionConstante:
ExpresionConstanteSimple [ OperadorComparador ExpresionConstanteSimple ]

ExpresionConstanteSimple:
Signo TerminoConstante { OperadorSumador TerminoConstante }

TerminoConstante:
FactorConstante { OperadorMultiplicador FactorConstante }

FactorConstante:
Identificador
ConstantePredefinida
Numero
Cadena
Caracter
( ExpresionConstante )
OperadorUnario FactorConstante

ParametrosDeLlamada:
( [ ListaDeExpresiones ] )

ListaDeExpresiones:
Expresion RestoListaDeExpresiones

RestoListaDeExpresiones:
_epsilon_
, Expresion RestoListaDeExpresiones

Expresion:
ExpresionSimple RestoExpresion 

RestoExpresion:
_epsilon_
OperadorComparador ExpresionSimple 

ExpresionSimple:
Signo Termino RestoExpresionSimple 

RestoExpresionSimple:
_epsilon_ 
OperadorSumador Termino

Signo:
_epsilon_
+ 
- 

Termino:
Factor RestoTermino

RestoTermino:
_epsilon_
OperadorMultiplicador Factor RestoTermino

Factor:
Identificador RestoFactor
ConstantePredefinida
Numero 
TipoPredefinido \{ ListaDeElementos \}
Cadena
Caracter
( Expresion )
OperadorUnario Factor
LlamadaFuncionPredefinida

RestoFactor:
\{ ListaDeElementos \}
ParametrosDeLlamada
ParteIzquierda

OperadorComparador:
= 
<> 
# 
< 
<= 
> 
>= 
IN 

OperadorSumador:
+ 
- 
OR 

OperadorMultiplicador:
* 
/ 
DIV 
MOD 
& 
AND 

OperadorUnario:
NOT 
~ 

//----------------------------------------------//
//		ELEMENTOS BASICOS			   	//
//----------------------------------------------//
TipoFormal:
[ ARRAY OF ] IdentificadorDeTipo

Numero:
NumeroEntero 
NumeroReal 

ListaDeElementos:
[ Elementos { , Elementos } ]

Elementos:
ExpresionConstante [ .. ExpresionConstante ]

// TIPOS
IdentificadorDeTipo:
TipoPredefinido 
Identificador

TipoPredefinido:
BITSET 
BOOLEAN 
CARDINAL 
CHAR 
INTEGER 
LONGINT 
LONGREAL 
PROC 
REAL 

// CONSTANTES
ConstantePredefinida:
TRUE _action_TRUE
FALSE _action_FALSE
NIL _action_NIL

ListaDeIdentificadores:
_action_marcaInicioLista Identificador RestoListaIdentificadores

RestoListaIdentificadores:
_epsilon_ _action_EliminarMarcaLista
, Identificador RestoListaIdentificadores

//----------------------------------------------------------------------------------//
//		IMPORTACIONES DE PROCEDIMIENTOS Y FUNCIONES PREDEFINIDAS			//
//----------------------------------------------------------------------------------//
// Los procedimientos predefinidos del lenguaje no se importan
IdentDeProc:
Identificador
IdentDeProcPredef

IdentDeProcPredef:
ALLOCATE
DEALLOCATE
ReadInt
WriteInt
ReadCard
WriteCard
ReadChar
WriteChar
ReadString
WriteString
ReadReal
WriteReal
WriteLn

//------------------------------------------------------------------------------//
//		LLAMADA DE PROCEDIMIENTOS Y FUNCIONES PREDEFINIDAS		//
//------------------------------------------------------------------------------//
LlamadaProcedimientoPredefinido:
DEC ( Expresion [ , Expresion ] )
INC ( Expresion [ , Expresion ] )
INCL ( Expresion , Expresion )
EXCL ( Expresion , Expresion )
NEW ( Expresion )
DISPOSE ( Expresion )
ReadInt ( Expresion )
WriteInt ( Expresion , Expresion )
ReadCard ( Expresion )
WriteCard ( Expresion , Expresion )
ReadChar ( Expresion )
WriteChar (  Expresion )
ReadString ( Expresion )
WriteString ( Expresion )
ReadReal ( Expresion )
WriteReal ( Expresion , Expresion [ , Expresion ] )
WriteLn
HALT

LlamadaFuncionPredefinida:
ABS ( Expresion )
CAP ( ParametroChar )
CHR ( Expresion )
FLOAT ( Expresion )
HIGH ( Expresion )
MAX ( IdentificadorDeTipo )
MIN ( IdentificadorDeTipo )
ODD ( Expresion )
ORD ( Expresion )
SIZE ( IdentificadorDeTipo )
TRUNC ( Expresion )
VAL ( IdentificadorDeTipo , Expresion )
